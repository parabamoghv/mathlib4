/-
Copyright (c) 2026 Amogh Parab. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Amogh Parab
-/
module

public import Mathlib.CategoryTheory.Monoidal.Discrete
public import Mathlib.Tactic.CategoryTheory.Monoidal.Basic
public import Mathlib.CategoryTheory.Monoidal.Rigid.Basic

/-!
# Categorical Groups

A categorical group is a monoidal category equipped with a negator,
and cancellation isomorphisms called the unit and counit isomorphisms.
The unit and counit isomorphisms must
satisfy coherence axioms.

With the coherence axioms, we can show that the negator extends to a functor and
the unit and counit isomorphisms are natural.

## Implementation note


We make `CategoricalGroup` as a typeclass with
MonoidalCategory, Groupoid, and RightRigidCategory as subclasses with no additional conditions.
Right rigidity gives the negator (right dual),
the counit morphism (coevaluation), and the unit morphism (evaluation).
With the groupoid structure, we can construct the unit and counit isomorphisms from the unit and
counit morphisms.

From RightRigidity, we also get the coherence axioms of the unit and counit isomorphisms
(evaluation-coevaluation and coevaluation-evaluation).
Again, with the groupoid structure, we extend these to
the coherence axioms of the unit and counit isomorphisms.

For consistency, we will use the terms "evaluation" and "coevaluation"
to refer to the unit and counit isomorphisms of a categorical group. This also avoids confusion
with the unit object.

## Future work

* Extend `negatorObj` to a functor `negator : C â¥¤ C` and
unit and counit isomorphisms to natural isomorphisms.
* Add basic lemmas.
* Extend categorical groups to symmetric categorical groups by adding a braiding.

## References

* John C. Baez and Aaron D. Lauda. Higher-dimensional algebra V: 2-groups. Theory
Appl. Categ., 12:423â€“491, 2004

-/

@[expose] public section

universe u v

namespace CategoryTheory

open Category MonoidalCategory CategoryTheory

namespace CategoricalGroup

variable {C : Type u} [Groupoid.{v} C]
  [MonoidalCategory.{v} C] [RightRigidCategory C]

/--
Negator of an object in a categorical group is the right dual of the object.
-/
def negatorObj (X : C) : C := Xá˜

/--
The unit (evaluation) isomorphism of a categorical group.
-/
def evaluationIso (X : C) : Xá˜ âŠ— X â‰… ðŸ™_ C where
  hom := Îµ_ X Xá˜
  inv := Groupoid.inv (Îµ_ X Xá˜)
  hom_inv_id := Groupoid.comp_inv (Îµ_ X Xá˜)
  inv_hom_id := Groupoid.inv_comp (Îµ_ X Xá˜)

/--
The counit (coevaluation) isomorphism of a categorical group.
-/
def coevaluationIso (X : C) : ðŸ™_ C â‰… X âŠ— Xá˜ where
  hom := Î·_ X Xá˜
  inv := Groupoid.inv (Î·_ X Xá˜)
  hom_inv_id := Groupoid.comp_inv (Î·_ X Xá˜)
  inv_hom_id := Groupoid.inv_comp (Î·_ X Xá˜)

/--
The zig-zag axiom 1: Elevating the coevaluation-evaluation axiom to an equality of isomorphism.
-/
lemma coevaluation_evaluation_iso (X : C) : 
    (whiskerLeftIso Xá˜ (coevaluationIso X)) â‰ªâ‰« (Î±_ Xá˜ X Xá˜).symm â‰ªâ‰« 
      whiskerRightIso (evaluationIso X) Xá˜ = Ï_ Xá˜ â‰ªâ‰« (Î»_ Xá˜).symm := by
  ext
  simp only [Iso.trans_hom, whiskerLeftIso_hom, Iso.symm_hom, whiskerRightIso_hom]
  exact ExactPairing.coevaluation_evaluation X Xá˜

/--
The zig-zag axiom 2: Elevating the evaluation-coevaluation axiom to an equality of isomorphism.
-/
lemma evaluation_coevaluation_iso (X : C) : (whiskerRightIso (coevaluationIso X) X) â‰ªâ‰«
(Î±_ X Xá˜ X) â‰ªâ‰«
whiskerLeftIso X (evaluationIso X)
=
(Î»_ X) â‰ªâ‰« (Ï_ X).symm := by
  ext
  simp only [Iso.trans_hom, whiskerRightIso_hom, Iso.symm_hom, whiskerLeftIso_hom]
  exact ExactPairing.evaluation_coevaluation X Xá˜



instance ExactPairing.of_rightDual_self (X : C) : ExactPairing Xá˜ X where
  coevaluation' := (evaluationIso X).inv

  evaluation' := (coevaluationIso X).inv

  coevaluation_evaluation' := by
    have : whiskerLeftIso X (evaluationIso X).symm â‰ªâ‰«
    (Î±_ X Xá˜ X).symm â‰ªâ‰« whiskerRightIso (coevaluationIso X).symm X
    = (Ï_ X) â‰ªâ‰« (Î»_ X).symm := by
      apply_fun (fun f => f.symm)
      Â· simp only [Iso.trans_symm, whiskerRightIso_symm, Iso.symm_symm_eq, whiskerLeftIso_symm,
        Iso.trans_assoc]
        exact evaluation_coevaluation_iso X
      Â· simp only [Iso.symm_bijective.injective]
    apply_fun (fun f => f.hom) at this
    simp only [Iso.trans_hom, whiskerRightIso_hom, Iso.symm_hom, whiskerLeftIso_hom] at this
    exact this


  evaluation_coevaluation' := by
    have : whiskerRightIso (evaluationIso X).symm Xá˜ â‰ªâ‰«
    (Î±_ Xá˜ X Xá˜) â‰ªâ‰« whiskerLeftIso Xá˜ (coevaluationIso X).symm
    = (Î»_ Xá˜) â‰ªâ‰« (Ï_ Xá˜).symm := by
      apply_fun (fun f => f.symm)
      Â· simp only [Iso.trans_symm, whiskerLeftIso_symm, Iso.symm_symm_eq, whiskerRightIso_symm,
        Iso.trans_assoc]
        exact coevaluation_evaluation_iso X
      Â· simp only [Iso.symm_bijective.injective]
    apply_fun (fun f => f.hom) at this
    simp only [Iso.trans_hom, whiskerLeftIso_hom, Iso.symm_hom, whiskerRightIso_hom] at this
    exact this

/--
In a categorical group, the right dual of an object is also its left dual.
-/
instance HasLeftDual.of_CategoricalGroup (X : C) : HasLeftDual X where
  leftDual := Xá˜
  exact := ExactPairing.of_rightDual_self X

instance LeftRigidCategory.of_CategoricalGroup : LeftRigidCategory C where
  leftDual := fun X => HasLeftDual.of_CategoricalGroup X

instance RigidCategory.of_CategoricalGroup : RigidCategory C where
  toRightRigidCategory := inferInstance
  toLeftRigidCategory := LeftRigidCategory.of_CategoricalGroup


end CategoricalGroup


end CategoryTheory
